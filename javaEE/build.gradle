apply plugin: 'java-library'

group 'com.smartdevicelink'
version new File(projectDir.path, ('/../VERSION')).text.trim()

sourceCompatibility = 1.7

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

repositories {
    google()
    jcenter()
}

// This extraLibs solution is explained here: https://discuss.gradle.org/t/how-to-include-dependencies-in-jar/19571/5
configurations {
    // configuration that holds jars to include in the jar
    extraLibs
}

dependencies {
    extraLibs fileTree(dir: 'libs', include: ['*.jar'])
    extraLibs 'org.mongodb:bson:3.10.1'
    extraLibs 'com.android.support:support-annotations:28.0.0'
    extraLibs 'org.java-websocket:Java-WebSocket:1.3.9'
    configurations.api.extendsFrom(configurations.extraLibs)
}

sourceSets {
    main.java.srcDirs += '../base/src/main/java'
    main.java.srcDirs += '../javaSE/src/main/java'
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

apply from: 'bintray.gradle'